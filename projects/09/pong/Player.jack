/** The Player object, a stick that moves up and down */
class Player {

    field int x, y; // x and y-axis position of the top left pixel of the player
    field int oldY; // old y-axis position of the top left pixel of the player 
    field int sizeX, sizeY; // pixel size of the player

    /** Constructs a new player */
    constructor Player new(int Ax, int Ay, int AsizeX, int AsizeY) {
        let sizeX = AsizeX;
        let sizeY = AsizeY;
        let x = Ax;
        let y = Ay;
        let oldY = Ay;
        do draw();
        return this;
    }
    
    /** Draws the player on the screen */
    method void draw() {
        if (y > oldY) {
            do Screen.setColor(true);
            do Screen.drawRectangle(x, oldY + sizeY, x + sizeX, y-1 + sizeY);
            do Screen.setColor(false);
            do Screen.drawRectangle(x, oldY, x + sizeX, y-1);
        }
        if (y < oldY) {
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + sizeX, oldY-1);
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y + sizeY, x + sizeX, oldY-1 + sizeY);
        }
        return;
    }

    /** Moves the player up and down */
    method void move(int step) {
        let oldY = y;
        let y = y + step;
        if (y < 0) {
            let y = 0;
        }
        if (y > 255) {
            let y = 255;
        }
        do draw();
        return;
    }

    /** Disposes this object */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}
