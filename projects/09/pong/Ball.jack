/** The Ball object, a circle that bounces around */
class Ball {

    field int x, y;        // x and y-axis position of the center of the ball
    field int oldX, oldY;  // old x and y-axis position of the center of the ball
    field int r;           // radius of the ball
    field int dx, dy;      // the x and y direction of the ball
    field PongGame theGame;    // reference to the game being played

    /** Constructs a new ball */
    constructor Ball new(int Aradius, int Ax, int Ay, int Adx, int Ady, PongGame game) {
        let r = Aradius;
        let x = Ax;
        let y = Ay;
        let oldX = Ax;
        let oldY = Ay;
        let dx = Adx;
        let dy = Ady;
        let theGame = game;
        do draw();
        return this;
    }

    /** Draws the ball on the screen */
    method void draw() {
        do Screen.setColor(false);
        do Screen.drawCircle(oldX, oldY, r);
        do Screen.setColor(true);
        do Screen.drawCircle(x, y, r);
        return;
    }

    /** Moves the ball */
    method void move() {
        let oldX = x;
        let oldY = y;
        let x = x + dx;
        let y = y + dy;
        if (theGame.collisionX(x, y, r)) {
            let x = x - dx;
            let dx = -dx;
        }
        if (theGame.collisionY(y,r)) {
            let y = y - dy;
            let dy = -dy;
        }
        return;
    }

    /** Restarts the ball in a new location */
    method void restart(int Ax, int Ay) {
        let oldX = x;
        let oldY = y;
        let x = Ax;
        let y = Ay;
        let dx = -dx;
        let dy = -dy;
        return;
    }

    /** Disposes this object */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}
